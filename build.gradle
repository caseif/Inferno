import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'checkstyle'

    id 'signing'

    id 'org.spongepowered.plugin' version '0.9.0'
    id 'net.minecrell.licenser' version '0.4.1'
    id 'com.github.johnrengelman.shadow' version '4.0.4'
}
defaultTasks 'clean', 'updateLicenses', 'build'

group = 'net.caseif.flint.inferno'
version = '1.3.2'

ext {
    projectName = 'inferno'
    description = 'The implementation of Flint minigame framework for the Sponge Minecraft server mod.'
    inceptionYear = '2016'
    packaging = 'jar'
    author = "Max Roncace"
}

configurations {
    shadow
    compile {
        extendsFrom shadow
    }
}

ext {
    versionSuffix = version.contains('SNAPSHOT') ? (
            (
                    System.getenv('GIT_COMMIT') ? ('-git('
                            + System.getenv('GIT_BRANCH')
                            .substring(System.getenv('GIT_BRANCH').split('/')[0].length() + 1) + '-'
                            + System.getenv('GIT_COMMIT').substring(0, 7) + ')') : ''
            )
                    + (System.getenv('BUILD_NUMBER') ? '-jnks' + System.getenv('BUILD_NUMBER') : '')
    ) : ''

    commonVersion = '1.3.6'
}

repositories {
	maven {
		name 'caseif'
		url 'http://repo.caseif.net/content/groups/public/'
	}
    maven {
        name 'sponge'
        url 'https://repo.spongepowered.org/maven/'
    }
    maven {
        name 'codemc'
        url 'https://repo.codemc.org/repository/maven-public'
    }
}

dependencies {
    compile 'org.spongepowered:spongeapi:7.1.0'
    shadow "net.caseif.flint.common:flintcommon:${commonVersion}"
    shadow 'org.bstats:bstats-sponge:1.5'

    checkstyle 'org.spongepowered:checkstyle:6.1.1-sponge1'
}

license {
    header file('HEADER.txt')
}

// Read source files using UTF-8
tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
    options.compilerArgs += '-Xlint:all'
}

// check code style
checkstyle {
    configProperties = [
            'name': project.name,
            'year': project.inceptionYear,
            'basedir': project.projectDir,
            'severity': 'warning'
    ]
    configFile = file('etc/checkstyle.xml')
}

jar {
    classifier = 'base'
}

jar.manifest.mainAttributes(
        'Created-By': System.properties['java.vm.version'] + " (" + System.properties['java.vm.vendor'] + ")",
        'Specification-Title': 'Flint',
        'Specification-Version': '1.3.2',
        'Specification-Vendor': 'Max Roncace',
        'Implementation-Title': name,
        'Implementation-Version': version + versionSuffix
)

processResources {
    from 'LICENSE.txt'
    filter { String line -> line.replace('SNAPSHOT', 'SNAPSHOT' + versionSuffix) }
}

shadowJar {
    configurations = [project.configurations.shadow]
    classifier = ''

    dependencies {
        include dependency('net.caseif.flint.common:flintcommon')
        include dependency('org.bstats:bstats-sponge')
    }

    relocate 'org.bstats', 'net.caseif.flint.inferno.lib.org.bstats'
}

task sourceJar(type: Jar) {
    from sourceSets.main.java
    from sourceSets.main.resources
    classifier = 'sources'
}

artifacts {
    archives shadowJar
    archives sourceJar
}

signing {
    sign configurations.archives
}

wrapper {
    gradleVersion = '4.10.3'
}
